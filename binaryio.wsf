<job id="binaryio">
<comment>binaryio verification program</comment>
<script language="VBScript" src="binaryio.vbs"/>
<script language="VBScript">
option explicit

class sample
	public objBinaryIO, intbyte, intnum, lngnum, str, uni, euc, datetime, bool, dbl

	private sub class_initialize
		set objBinaryIO = new binaryio

		intbyte = cbyte(0)
		intnum = 0
		lngnum = clng(0)
		str = ""
		uni = ""
		euc = ""
		datetime = cdate("1970/01/01")
		bool = false
		dbl = 0.0
	end sub

	public sub deserialize
		with objBinaryIO
			if true then
				.deserialize intbyte
				.deserialize intnum
				.deserialize lngnum
				.deserialize str
				uni = objUTF8.getstring(.deserialize_bytes(-1))
				euc = tostring("euc-jp", .deserialize_bytes(-1))
				.deserialize datetime
				.deserialize dbl
				.deserialize bool
			else
				intbyte = .deserialize_byte
				intnum = .deserialize_int
				lngnum = .deserialize_lng
				str = tostring(null, .deserialize_bytes(-1))
				uni = objUTF8.getstring(.deserialize_bytes(-1))
				euc = tostring("euc-jp", .deserialize_bytes(-1))
				datetime = .deserialize_date
				dbl = .deserialize_double
				bool = .deserialize_bool
			end if
		end with
	end sub

	public sub read(filepath)
		objBinHex.nodetypedvalue = readbinaryfile(filepath)
		objBinaryIO.buffer = objBinHex.text
		deserialize
	end sub

	public function serialize
		with objBinaryIO
			if true then
				.serialize intbyte
				.serialize intnum
				.serialize lngnum
				.serialize str
				.serialize_bytes objUTF8.getbytes_4(uni)
				.serialize_bytes fromstring("euc-jp", euc)
				.serialize datetime
				.serialize dbl
				.serialize bool
			else
				.serialize_byte intbyte
				.serialize_int intnum
				.serialize_lng lngnum
				.serialize_bytes fromstring(null, str)
				.serialize_bytes objUTF8.getbytes_4(uni)
				.serialize_bytes fromstring("euc-jp", euc)
				.serialize_date datetime
				.serialize_double dbl
				.serialize_bool bool
			end if
		end with
	end function

	public sub save(filepath)
		objBinaryIO.buffer = ""
		serialize
		objBinHex.text = objBinaryIO.buffer
		savebinarydata filepath, objBinHex.nodetypedvalue
	end sub

	public sub print
		wscript.echo intbyte
		wscript.echo intnum
		wscript.echo lngnum
		wscript.echo str
		wscript.echo uni
		wscript.echo euc
		wscript.echo datetime
		wscript.echo dbl
		wscript.echo bool
	end sub
end class

dim objsample

set objsample = new sample
with objsample
	.intbyte = cbyte(123)
	.intnum = 12345
	.lngnum = 123456789
	.str = "hello, world! はい"
	.uni = "ユニコード"
	.euc = "いろんな文字コード"
	.datetime = now()
	.bool = true
	.dbl = 3.14159265359
	.save wscript.scriptfullname & ".dat"
end with

set objsample = new sample
with objsample
	.intbyte = cbyte(0)
	.read wscript.scriptfullname & ".dat"
	.print
end with
</script>
</job>
